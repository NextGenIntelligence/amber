# Copyright (C) 2012 nineties
#
# $Id: hyperlisp/syntax.ab 2013-02-12 17:33:06 nineties $

# Reference
#   Masahiko Sato and Masami Hagiya: HyperLisp, Algorithmic Languages
#   (J.W. de Bakker and J.C. van Vliet eds.), North-Holland, 1981,
#   pp.251-269.

import core

module HyperLisp {
    # Basic syntax
    hl_simple_expr
        ::= "(" hl_expr "." hl_expr ")"      { cons($1, $3)  }
          | "[" hl_expr "." hl_expr "]"      { snoc($1, $3)  }
          | "(" delimited(hl_expr, [,;]) ")" { cons_list($1) }
          | "[" delimited(hl_expr, [,;]) "]" { snoc_list($1) }
          | "(" ")"                          { zero }
          | "[" "]"                          { zero }
          | "0"                              { zero }
          | "1"                              { one  }

    hl_primary_expr
        ::= "'" hl_primary_expr { snoc(one, snoc($1, zero)) }
          | hl_simple_expr

    hl_secondary_expr
        ::= hl_secondary_expr "(" delimited(hl_expr, [,;]) ")"
            { cons_list(std::cons($0, $2)) }
          | hl_secondary_expr "[" delimited(hl_expr, [,;]) "]"
            { snoc_list(std::cons($0, $2)) }
          | hl_primary_expr

    hl_expr
        ::= hl_secondary_expr ":" hl_secondary_expr
            { cons_list([$0, $2]) }
          | hl_secondary_expr

    # Pretty printing
    is_cons_list 
        : x where x == zero -> true
        | e@Cons            -> is_cons_list(e.cdr)
        | _                 -> false
    is_snoc_list
        : x where x == zero -> true
        | e@Snoc            -> is_snoc_list(e.cdr)
        | _                 -> false

    pprint
        : (io, x) where x == zero -> print(io, "0")
        | (io, x) where x == one  -> print(io, "1")
        | (io, e@Cons) where is_cons_list(e)
            -> print(io, "("); pprint_cons_list(io, e); print(io, ")")
        | (io, e@Snoc) where is_snoc_list(e)
            -> print(io, "["); pprint_cons_list(io, e); print(io, "]")
        | (io, e@Cons) -> printf(io, "(%p . %p)", e.car, e.cdr)
        | (io, e@Snoc) -> printf(io, "[%p . %p]", e.car, e.cdr)

    pprint_cons_list
        : (io, x) where x == zero  -> nil
        | (io, e@Cons) where e.cdr == zero -> pprint(io, e.car)
        | (io, e@Snoc) where e.cdr == zero -> pprint(io, e.car)
        | (io, e@Cons)
          -> pprint(io, e.car); print(io, ", "); pprint_cons_list(io, e.cdr)
        | (io, e@Snoc)
          -> pprint(io, e.car); print(io, ", "); pprint_cons_list(io, e.cdr)
}
