;
; rowl - generation 1
; Copyright (C) 2010 nineties
;
; $Id: vm-insn.rlc 2010-06-11 00:56:13 nineties $
;

; rowlVM register usage
;   %eax, %ebx : general purpose register
;   %ecx       : program counter
;   %edx       : zero register
;   %esi       : stack pointer

(define gen_comparison (cmovname) `(
    (asm "movl $2, %eax")
    (vmsbyte 1 %ebx)
    (asm "cmpl %edx, (%esi)")
    (asm @cmovname " %ebx, %eax")
    (vmsucc %eax)
    (vmfetch)))

(var vm_instructions `(
    (exit       1 @true ((vmpop %eax) (break)))
    (nop        1 @true ())
    (imm_i0     1 @true ((vmpush %edx)))
    (imm_i1     1 @true ((vmpush $1)))
    (imm_i2     1 @true ((vmpush $2)))
    (imm_i3     1 @true ((vmpush $3)))
    (imm_i4     1 @true ((vmpush $4)))
    (imm_i5     1 @true ((vmpush $5)))
    (imm_im1    1 @true ((vmpush $-1)))
    (imm_int    2 @true ((vmsbyte 1 %eax) (vmpush %eax)))
    (iadd       1 @true ((vmbin "addl")))
    (isub       1 @true ((vmbin "subl")))
    (imul       1 @true ((vmpop %eax) (asm "imul (%esi), %eax") (asm "movl %eax, (%esi)")))
    (idiv       1 @true (
        (vmpop %ebx)
        (vmpop %eax)
        (asm "idiv %ebx")
        (vmpush %eax)
        (asm "xorl %edx, %edx") ; re-clear zero register
        ))
    (imod       1 @true (
        (vmpop %ebx)
        (vmpop %eax)
        (asm "idiv %ebx")
        (vmpush %edx)
        (asm "xorl %edx, %edx") ; re-clear zero register
        ))
    (goto       2 @nil  ((vmsbyte 1 %eax) (asm "addl %eax, %ecx") (vmfetch)))
    (if_zero    2 @nil  @(gen_comparison "cmovz"))
    (if_nonzero 2 @nil  @(gen_comparison "cmovnz"))
    (if_gt      2 @nil  @(gen_comparison "cmovg"))
    (if_ge      2 @nil  @(gen_comparison "cmovge"))
    (if_lt      2 @nil  @(gen_comparison "cmovl"))
    (if_le      2 @nil  @(gen_comparison "cmovle"))
    (drop       1 @true ((asm "addl $4, %esi")))
    (drop2      1 @true ((asm "addl $8, %esi")))
    (dup        1 @true ((asm "movl (%esi), %eax") (vmpush %eax)))
    (vcall      3 @nil  (
        (vmubyte 1 %eax)
        (asm "addl $3, %ecx")
        (vmpush %ecx)   ; store return point
        (vmpush %edi)   ; store base pointer
        (asm "movl %esi, %edi") ; set base pointer
        (asm "movl funtable(,%eax,4), %ecx")
        (vmfetch)
        ))
    (ireturn    1 @nil  (
        (asm "movl (%esi), %ebx") ; return value
        (asm "movl %edi, %esi")
        (vmpop %edi)
        (vmpop %ecx)
        (vmubyte -1 %eax) ; total size of arguments
        (asm "addl %eax, %esi")
        (vmpush %ebx) ; push return value
        (vmfetch)
        ))
    ))
