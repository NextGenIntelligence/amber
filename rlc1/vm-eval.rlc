;
; rowl - generation 1
; Copyright (C) 2010 nineties
;
; $Id: vm-eval.rlc 2010-07-10 01:32:39 nineties $
;

; rowlVM register usage
;   %eax, %ecx : general purpose register
;   %ebx       : program counter
;   %edx       : zero register
;   %esi       : stack pointer
;   %edi       : base pointer

(import "stdlib")
(import "vm-compile")
(import "vm-insn")
(import "vm-prim")

(eprintln "# of VM instructions = " (length vm_instructions))

(var insn_entries ())
(do
    (foreach e vm_instructions (do
        (var label (mklabel (car e)))
        (var len (cadr e))
        (var code (cadddr e))
        (if (caddr e) (set code (append code `((vmsucc @len) (vmfetch)))))
        (set insn_entries (cons `(@label @code) insn_entries))
        ))
    (set insn_entries (reverse insn_entries))
)

(define emit_vmpush ((_ val) _ _) (do
    (emit "subl" 4 `%esi)
    (movl val `(deref %esi 0))
    ))

(define emit_vmpop ((_ reg) _ _) (do
    (movl `(deref %esi 0) reg)
    (emit "addl" 4 `%esi)
    ))

(define emit_vmsucc ((_ len) _ _)
    (if (== len 1) (emit "incl" `%ecx) (emit "addl" len `%ecx))
    )

(define emit_vmfetch ((_) _ _) (do
    (emit "movzbl" `(deref %ecx 0) `%eax)
    (emit "jmp *insn_table(,%eax,4)")
    ))

(define emit_vmubyte ((_ offs reg) _ _) (do
    (emit "movzbl" `(deref %ecx @offs) reg)
    ))

(define emit_vmsbyte ((_ offs reg) _ _) (do
    (emit "movsbl" `(deref %ecx @offs) reg)
    ))

(define emit_vmushort ((_ offs reg) _ _) (do
    (emit "movzwl" `(deref %ecx @offs) reg)
    ))

(define emit_vmsshort ((_ offs reg) _ _) (do
    (emit "movswl" `(deref %ecx @offs) reg)
    ))

(define emit_vmlong ((_ offs reg) _ _) (do
    (emit "movl" `(deref %ecx @offs) reg)
    ))

(define emit_vmarg ((_ offs reg) _ _) (do
    (emit "movl" `(deref %edi @(* 4 (+ offs 2))) reg)
    ))

; switch implementation using table jump
(define emit_vmtable ((_ tblname body) _ ret) (do
    ; table area
    (change_area `rodata)
    (emit ".align 4")
    (emit ".align 4")
    (print_label tblname)
    (foreach case body (emit ".long" (car case)))

    ; code area
    (push_break_point)
    (change_area `text)
    (foreach case body (do
        (print_label (car case))
        (emit_block (cadr case) nil)
        ))
    (emit_break_point)
    (pop_break_point)

    (if ret (emit_return `(return) nil nil))
    ))

(define emit_vmbin ((_ asmcode) _ ret) (do
    (emit_expr `(vmpop %eax) nil nil)
    (emit_expr `(asm @asmcode " %eax, (%esi)") nil ret)
    ))

(define emit_vmsave ((_) _ _) (do
    (pushl `%ecx)
    ))

(define emit_vmrestore ((_) _ _) (do
    (popl `%ecx)
    (movl 0 `%edx)
    ))

(add_new_expr `vmpush    emit_vmpush)
(add_new_expr `vmpop     emit_vmpop)
(add_new_expr `vmsucc    emit_vmsucc)
(add_new_expr `vmfetch   emit_vmfetch)
(add_new_expr `vmubyte   emit_vmubyte)
(add_new_expr `vmsbyte   emit_vmsbyte)
(add_new_expr `vmushort  emit_vmushort)
(add_new_expr `vmsshort   emit_vmsshort)
(add_new_expr `vmlong    emit_vmlong)
(add_new_expr `vmtable   emit_vmtable)
(add_new_expr `vmbin     emit_vmbin)
(add_new_expr `vmarg     emit_vmarg)
(add_new_expr `vmsave    emit_vmsave)
(add_new_expr `vmrestore emit_vmrestore)

(var vm-eval-code `(

(export stack_size)
(export stack)

(int  stack_size) ; in bytes
(int* stack)

(export value_area_base)
(void* value_area_base)
(export object_area_base)
(void* object_area_base)

(export cmd_argc)
(int cmd_argc)
(export cmd_argv)
(void* cmd_argv)

(void*[] 65536 funtable @(map caddr prim_table))

(export init_vmstack)
(fun init_vmstack (stsize) (
    (= stack (alloc_block stsize))
    (= stack_size stsize)
    ))

(export vm_eval)
(fun vm_eval (argc argv insn nref) (
    (= cmd_argc argc)
    (= cmd_argv argv)
    (asm "pushl %ebx")
    (asm "pushl %esi")
    (asm "movl stack, %esi")
    (asm "movl stack_size, %eax")
    (asm "addl %eax, %esi")
    (asm "movl " insn ", %ecx")
    (asm "xorl %edx, %edx") ; initialize zero register

    ; allocate area for global reference objects
    (asm "movl " nref ", %eax")
    (asm "shll $2, %eax")
    (asm "subl %eax, %esi")
    (asm "movl %esi, %edi") ; set base pointer

    (vmfetch)
    (vmtable insn_table @insn_entries)
    (asm "popl %esi")
    (asm "popl %ebx")
    ))

(fun vm_extcall (idx argc argv) (
    (for i 0 argc (
        (int arg ([] argv (- (- argc i) 1)))
        (asm "pushl " arg)
        ))
    (int ret (indcall ([] funtable idx)))
    (int off (* 4 argc))
    (asm "addl " off ", %esp")
    (return ret)
    ))
))

(vtable_push)
(compile vm-eval-code)
(compile vm-prim-code)
(vtable_pop)
