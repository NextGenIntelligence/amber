# Copyright (C) 2014 nineties
# $Id: tuple.ab 2014-04-09 18:50:36 nineties $

#= Tuple =
# An n-tuple is an ordered collection of n objects. Tuples do not have
# functionalities as a sequence like lists or arrays. For example, you
# can't change the size of a tuple. Alternatively, tuples are very light
# and element-access is faster than other containers.
#
# The most commonly used idiom with tuples is *multivariate function*.
# You can return multiple values as a tuple and pick them up using
# pattern matching assignment.
# ----
# > f(): return (1,2,3)
# > (x, y, z): f()
# > x              # => 1
# > y              # => 2
# > z              # => 3
# ----

Tuple: object_system.builtin_class('Tuple) with {
    # Create n-ary tuple. Its fields are initialized by v.
    .new(v, n @ Int): prim.tuple_new(v, n)

    # Create a tuple from iterable sequence.
    .new(container) when container.Iterable? :
        prim.list_to_tuple(container.to_list())

    # The number of elements.
    .size:   alias(() -> prim.arity(self))
    .length: alias(() -> prim.arity(self))

    # Apply `f` to each element of `self`.
    .map(f):  prim.tuple_map(f, self)
    .map!(f): prim.tuple_map(f, self)

    extend Iterable
    .each(): {
        tuple: self
        idx: 0
        size: self.size

        iterator () -> {
            if (idx >= size)
                return nil
            idx += 1
            tuple[idx-1]
        }
    }
}
prim.set_builtin_parent('Tuple, Tuple)
