# Copyright (C) 2014 nineties
# $Id: compile.ab 2014-04-23 17:32:11 nineties $

#= Ahead-of-time compilation =

import interactive (ask)

# Now, we a in the directory where this file is installed.
# Change working directory to the original path.
import sys
sys::chdir(INVOKE_PATH)

if ARGV.empty? {
    stderr.puts("Usage: amber --compile <file>")
    exit(1)
}

# Construct a state object for compilation.
Compiler := `Compiler{}

# Detect input and output languages.
languages := Table{
    ("ab", "amber"),
    ("c",  "CAST")
}

detect_lang(f, type) := {
    idx := nil
    lang := nil
    (idx = f.last_index(".")) and (lang = languages[f[idx+1 .. -1]])
    return lang if lang
    ask("What is the %s language? %p", type, languages.values)
}

load_lang(f, type) := {
    l := detect_lang(f, type).to_sym()
    evalfull(`(Import{[!l]}))
}

Compiler.input_file      := ARGV[0]
if not amber.output_file
    amber.output_file := ask("What is the name of the output file?")
Compiler.output_file     := amber.output_file
Compiler.input_language  := load_lang(ARGV[0], "input")
Compiler.output_language := load_lang(amber.output_file, "output")

# Logging
Compiler.log(level @ Int, fmt @ String, args...) := {
    if level <= amber.verbose_level {
        printf(fmt, args...)
        stdout.flush()
    }
}
Compiler.log(fmt @ String, args...) := self.log(0, fmt, args...)

# Parse the inputfile as a program of given language.
Compiler.program := {
    prog := nil
    fp   := io::open_in(ARGV[0])
    try {
        prog = parser::parse(Compiler.input_language, fp)
    } catch e -> {
        fp.close()
        throw e
    }
    fp.close()
    prog
}

evalfull(`(Import{[!Compiler.output_language.name, compiler]}))
exit(0)
