# Copyright (C) 2014 nineties
# $Id: String.ab 2014-03-14 17:40:38 nineties $

#= Strings. =
# The representation of strings are internally null-terminated sequence
# of 8-bit ascii characters. Multibyte characters are not supported.
# Amber does not have any specific type of objects for 'characters'.
# We treat a characters as a string of length one.
#
# Amber's string objects are immutable. So several operations such as
# concatenation of many strings are not efficient. Use `StringIO` module
# for such operations.

typecast(a, 'String) = a.to_s()
typejoin(a, 'String) = 'String
typejoin(b, 'String) = 'String

# Concatination of two strings.
# ----
# > "Hello" + " " + "World"     # => "Hello World"
# ----

add = Prim.string_add | add

# Repeation of string.
mul(s @ String, n @ Int) = Prim.string_repeat(s, n)
mul(n @ Int, s @ String) = Prim.string_repeat(s, n)

trait String {
    # Number of characters.
    # ----
    # > "Hello World".length    # => 21
    # ----
    .length: alias(() -> Prim.string_length(self))
    .size:   alias(() -> Prim.string_length(self))

    # `i`-th character.
    .get(i @ Int): Prim.string_at(self, i)

    # substring of length `m` from `i`-th position.
    .get(i @ Int, m @ Int): Prim.string_substr(self, i, m)

    # `true` when the string contains `s` as a substring.
    .contain?(s): Prim.string_contain?(self, s)

    # Convert special ascii characters to escape-sequences.
    # ----
    # > "Hello World\n".escape()      # => "Hello World\\n"
    # ----
    .escape(): Prim.string_escape(self)

    # Convert escape-sequences to corresponding characters.
    # ----
    # > "Hello World\\n".unescape()   # => "Hello World\n"
    # ----
    .unescape(): Prim.string_unescape(self)

    # Split the string by delimiter string `d` and return a list.
    # ----
    # > "Hello World".split("o")      # => ["Hell", " W", "rld"]
    # ----
    .split(d): Prim.string_split(self, d)

    .ljust(w): Prim.string_ljust(self, w, "")
    .ljust(w, punct): Prim.string_ljust(self, w, punct)

    .rjust(w): Prim.string_rjust(self, w, "")
    .rjust(w, punct): Prim.string_rjust(self, w, punct)

    .center(w): Prim.string_center(self, w, "")
    .center(w, punct): Prim.string_center(self, w, punct)

    # Convert a character (i.e. a string of length one) to its ascii code.
    # Exception:
    #   * InvalidArgument
    # ----
    # > "a".to_code()       # => 97
    # ----
    .to_code(): Prim.char_to_code(self)

    .to_s(): self

    # Parse the string as a decimal integer.
    # Exception:
    #   * ValueError: Invalid integer format.
    .to_i(): Prim.string_to_int(self)

    # Parse the string as a floating-point number.
    # Exception:
    #   * ValueError: Invalid floating-point number format.
    .to_f(): Prim.string_to_float(self)

    #== Iteration ==
    extend 'Iterable
    .each(): {
        str: self
        len: self.size
        idx: 0

        iterator () -> {
            if (idx == len)
                return nil
            idx += 1
            return str[idx-1]
        }
    }
}

trait Int {
    # Convert ascii code to a character.
    # Exception:
    #   * InvalidArgument
    .to_char(): Prim.code_to_char(self)
}

Prim.set_builtin_parent('String, Trait.String)
