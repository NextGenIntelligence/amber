# Copyright (C) 2010 nineties
# $Id: Module.ab 2014-02-01 15:43:11 nineties $

Module: enter_module(\Module)

    # import mod.var to global module.
    import_variable(mod @ Module, var @ Symbol):
        Prim.define_variable(var, Prim.get_slot(mod, var), \incremental)

    import_variables(mod @ Module, vars @ List):
        Prim.list_map(v -> import_variable(mod, v), vars)

exit_module()

macro(Import{symbols}): {
    script:
        Prim.string_add(
        Prim.string_join(
            Prim.list_map(Prim.symbol_to_string, symbols), "/")
        , ".ab")

    mod: Prim.list_foldl1((l, r) -> `Slot{!l, !r}, symbols)

    expand_import:
        [M] -> `Seq{[
            ((!M):enter_module(\!M)),
            If{not Prim.has_slot?(!M, \exports), ((!M).exports=[])},
            (private: enter_module(\private)),
            load(!script),
            exit_module(),
            exit_module()
        ]}
        | [M,rest...] -> `Seq{[
            ((!M):enter_module(\!M)),
            If{not Prim.has_slot?(!M, \exports), ((!M).exports=[])},
            !expand_import(rest),
            exit_module()
        ]}

    `Seq{[
        !expand_import(symbols),
        Module.import_variables(!mod, (!mod).exports)
    ]}
}

macro(Export{vars}): {
    `Seq{[
        Seq{!Prim.list_map(v -> `Prim.set_slot(private.proto, \!v, !v), vars)},
        `(exports = Prim.list_foldr(Prim.list_insert_unique, \!vars, exports))
    ]}
}
