# Copyright (C) 2014 nineties
# $Id: symbol.ab 2014-04-18 23:44:46 nineties $

import numeric (compare)

# A symbol is an object which can be used as the head of an object, a
# variable, the name of a slot and so on. Amber uses the special symbol
# `Symbol` for the head of symbol objects. The name of a symbol must be
# read as a usual identifier, i.e. [A-Za-z_][A-Za-z0-9_]*[!?]? in regular
# expression.
#
# All symbols except ones which created by `Symbol.unique` are internalized
# to Amber system. In other words, symbols which have same name are always
# same object. Therefore, several functions for symbols are faster than
# ones for strings, e.g. `equal?`, `compare` and `hash`.

trait Symbol with {
    .to_sym() := self

    # The unique identification number.
    .id := alias(() -> prim.symbol_id(self))

    # Convert lower characters to upper ones.
    .upcase() := str(self).upcase().to_sym()

    # Convert upper characters to lower ones.
    .downcase() := str(self).downcase().to_sym()

    # Convert first character to uppercase and others to lowercase.
    .capitalize() := str(self).capitalize().to_sym()
}

prim.set_builtin_parent('Symbol, Symbol)

# Create a symbol, which is not internalized, with given name.
unique := {
    i := 0

    () -> {
          name := "abcdefghijklmnopqrstuvwxyz"[i]
          i = (i + 1) % 26
          prim.unique_symbol(prim.string_add("%", name).to_sym())
    } | (name) -> prim.unique_symbol(name.to_sym())
}

# Extend the comparison operator.
# NB: This function compares id-numbers of symbols, not lexicographic.
compare := prim.symbol_compare | compare

# Convert to a string.
str(s @ Symbol) := prim.symbol_to_string(s)
