" Copyright (C) 2014 nineties "
" $Id: interpret.ab 2014-04-17 03:20:40 nineties $ "

" Entry-point of Amber intepreter "

" NB: We write comments as string literals until we define comment syntax. "

Apply{Slot{prim, load}, List{"boot.ab"}}

# Now, we a in the directory where this file is installed.
# Change working directory to the original path.
import sys
sys::chdir(INVOKE_PATH)

if (ARGV.empty?) {
    # Start shell

    # Change directory to the ditectory where amber is invoked.
    prim.chdir(amber::INVOKE_PATH)

    # Add `exit` command.
    amber::eval('exit) := exit(0)

    # Enable syntax for shell.
    amber::enable_shell_syntax()
    amber::shell_outputs := Array.new()
    amber::shell_outputs.push(nil)

    PROMPT = "amber"
    p := Parser.new(stdin, amber)

    while true {
        try {
            p.line = amber::shell_outputs.size
            if (p.parse('shell)) {
                LOCATION = p.loc
                v := amber::evalfull(p.tree)
                printf("=> %s\n", pretty(v, indent=3))
                amber::shell_outputs[0] = v
                amber::shell_outputs.push(v)
            } else {
                stderr.printf("%s Syntax error: %s\n",
                        pretty(p.loc), p.error_string)
                p.reset!()
            }
        } catch e -> {
            stderr.printf("%s Exception: %s\n",
                    pretty(p.loc), pretty(e, compress=true))
            p.reset!()
        }
    }
} else {
    try {
        load(ARGV[0])
    } catch e -> {
        stderr.printf("%s Exception: %s\n",
                pretty(LOCATION), pretty(e))
        exit(1)
    }
    exit(0)
}
