;
; rowl - 1st generation
; Copyright (C) 2010 nineties
;
; $Id: stdlib.rlc 2012-06-14 19:12:40 nineties $
;

(var rewrite (macro (name params body) `(var @name (macro @params @body))))
(rewrite define (name params body) `(var @name (lambda @params @body)))

;=== aliases ===
(var otherwise true)

(var + add)
(var - sub)
(var * mul)
(var / div)
(var % mod)
(var ~ bitnot)
(var & bitand)
(var | bitor)
(var ^ bitxor)
(var << lshift)
(var >> rshift)
(var == eq)
(var != ne)
(var < lt)
(var > gt)
(var <= le)
(var >= ge)
(var = set)

;=== utility ===
(define nil? (a) (if a nil true))
(var not nil?)

(rewrite incr (x) `(set @x (+ @x 1)))
(rewrite decr (x) `(set @x (- @x 1)))

(rewrite += (x y) `(set @x (+ @x @y)))
(rewrite -= (x y) `(set @x (- @x @y)))

(define [] (idx container)
    (cond
        ((string? container) (string_get container idx))
        ((array? container) (array_get container idx))
        (otherwise (eprintln "invalid container for operator [] : " container))
        ))

(rewrite and (lhs rhs) `(if @lhs @rhs nil))
(rewrite or  (lhs rhs) `(if @lhs true @rhs))

(define tos (val)
    (cond
        ((string? val) val)
        ((char? val)   (char2s val))
        ((int? val)    (int2s val))
        ((symbol? val) (symbol2s val))
        (otherwise (eprintln "invalid application of `tos` for " val))
        ))

(define ++ args (do
    (set args (map tos args))
    (sappend . args)
    ))

(define s++ args (tosym (++ . args)))

;=== loops ===
(rewrite upto (it beg end body) `(do
    (var @it @beg)
    (var end_ @end)
    (while (<= @it end_) (do
        @body
        (set @it (+ @it 1))
        )
    )))

(rewrite downto (it beg end body) `(do
    (var @it @beg)
    (var end_ @end)
    (while (>= @it end_) (do
        @body
        (set @it (- @it 1))
        )
    )))

;=== list operations ===
(define cadr   (list) (car (cdr list)))
(define caddr  (list) (car (cdr (cdr list))))
(define caaddr (list) (car (car (cdr (cdr list)))))
(define cadddr (list) (car (cdr (cdr (cdr list)))))
(define caar   (list) (car (car list)))
(define cddr   (list) (cdr (cdr list)))
(define cdddr  (list) (cdr (cdr (cdr list))))
(define cdar   (list) (cdr (car list)))
(define nth    (idx list) (if (== idx 0) (car list) (nth (- idx 1) (cdr list))))

(rewrite push (list v) `(set @list (cons @v @list)))

(define member? (val list)
    (cond
        ((nil? list) nil)
        ((== val (car list)) true)
        (otherwise (member? val (cdr list)))
        ))

(define map (lam list) (if (nil? list) nil (cons (lam (car list)) (map lam (cdr list)))))

;=== I/O functions ===

(define println params (do
    (print . params)
    (print '\n')
    ))

(define eprintln params (do
    (eprint . params)
    (eprint '\n')
    ))

(define error params (do
    (eprint "ERROR: ")
    (eprintln . params)
    (exit 1)
    ))
