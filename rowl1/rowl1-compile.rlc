;
; rowl - 1st generation
; Copyright (C) 2010 nineties
;
; $Id: rowl1-compile.rlc 2010-12-30 20:12:49 nineties $
;

(import "rlvm-compile")
(import "rowl1-types")

(compile `object `(

(import "rowl1-util")
(import "rowl1-node")
(import "rowl1-symtable")
(import "rowl1-assemble")

(var function (to_sym "%function"))
(var argument (to_sym "%argument"))
(var getnth   (to_sym "%getnth"))

(var vartable (make_symtable))

(export fun register_symbol (sym type) (
    (if (== (symtable_find vartable sym 0) type) return)
    (symtable_add vartable sym 0 type)
    ))

(export fun compile_function (name args body) (

    (register_symbol name function)
    (symtable_push vartable)

    (var asm (make_assembler))
    (= args (setup_args args))

    (compile_expression asm body @TRUE)

    (symtable_pop vartable)
    (return (cons args (get_code asm)))
    ))

(fun make_arg (idx) (
    (return (make_expr argument (list1 (box idx))))
    ))

(fun make_nth (obj idx) (
    (return (make_expr getnth (list2 obj (box idx))))
    ))

(fun setup_args (args) (
    (var orig args)
    (var idx 0)
    (while args (do
        (setup_arg (make_arg idx) (car args))
        (= args (cdr args))
        (+= idx 1)
        ))
    (return orig)
    ))

(fun setup_arg (obj arg) (
    (var argty (expr_head arg))
    (if (|| (== argty Int) (== argty String))
        return
        )
    (if (== argty Symbol) (do
        (symtable_add vartable arg 0 obj)
        return
        ))
    (if (== argty HeadP) (do
        (var head (expr_arg_symbol arg 0))
        (var v (expr_arg_symbol arg 1))
        (symtable_add vartable v 0 obj)
        return
        ))
    (if (== argty List) (do
        (var idx 0)
        (while arg (do
            (setup_arg (make_nth obj idx) (car arg))
            (= arg (cdr arg))
            (incr idx)
            ))
        return
        ))
    (output_error stderr)
    (output_string stderr "invalid argument pattern `")
    (pretty_print_abbrev stderr arg)
    (output_string stderr "'\n")
    (exit 1)
    ))

(extern fun lookup_func)
(export fun compile_expression (asm expr ret) (
    (var hd (expr_head expr))
    (if (== hd Symbol) (do
        (var v (symtable_find vartable expr 0))
        (if (! v) (do
            (output_error stderr)
            (output_string stderr "undefined variable `")
            (output_symbol stderr expr)
            (output_string stderr "'\n")
            (exit 1)
            ))
        (if (== v function) (do
            (put_push asm expr)
            (if ret (put_ireturn asm))
            return
            ))
        (compile_operand asm v)
        (if ret (put_ireturn asm))
        return
        ))
    (if (== hd Int) (do
        (put_imm_int asm expr)
        (if ret (put_ireturn asm))
        return
        ))
    (if (== hd String) (do
        (put_push asm expr)
        (if ret (put_ireturn asm))
        return
        ))
    (if (== hd List) (do
        (if (== expr 0)
            (put_imm_i0 asm)
            (do
                (compile_expression asm (cdr expr) @FALSE)
                (compile_expression asm (car expr) @FALSE)
                (put_cons asm)
            ))
        (if ret (put_ireturn asm))
        return
        ))
    (compile_call asm hd (expr_args expr) ret)
    ))

(fun compile_operand (asm desc) (
    (var ty (expr_head desc))
    (if (== ty argument) (do
        (put_arg asm (expr_arg_int desc 0))
        return
        ))
    (if (== ty getnth) (do
        (compile_operand asm (expr_arg desc 0))
        (put_nth asm (expr_arg_int desc 1))
        return
        ))
    ))

(export fun compile_call (asm fun args ret) (
    (= args (list_reverse args))
    (var arity 0)
    (while args (do
        (compile_expression asm (car args) @FALSE)
        (= args (cdr args))
        (incr arity)
        ))

    (if (== (node_type fun) @SymbolE)
        (do
            (var code (lookup_func fun arity))
            (put_push asm code)
            (put_jcall asm (* 4 arity))
        )
        (do
            (put_imm_int asm arity)
            (compile_expression asm fun @FALSE)
            (put_imm_int32 asm lookup_func)
            (put_dcall asm 8)
            (put_jcall asm (* 4 arity))
        ))

    (if ret (put_ireturn asm))
    ))

(export fun compile_simple_function (asm arity func ret) (
    (var n arity)
    (while (> n 0) (do
        (decr n)
        (put_arg asm n)
        ))
    (compile_simple_call asm arity func ret)
    ))

(export fun compile_simple_call (asm arity func ret) (
    (if @(IS_PRIM func)
        (do
            (put_pcall asm @(PRIM_IDX func) (* 4 arity))
            (if ret (put_ireturn asm))
        )
        (do
            (put_imm_int32 asm func)
            (put_dcall asm (* 4 arity))
            (if ret (put_ireturn asm))
        ))
    ))

    ))
