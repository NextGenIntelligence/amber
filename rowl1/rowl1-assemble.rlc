;
; rowl - 1st generation
; Copyright (C) 2010 nineties
;
; $Id: rowl1-assemble.rlc 2011-09-26 15:06:29 nineties $
;

; -- Just in time assembler
; (byte code (cvector), labels (ivector), replaces (list of repl info),
;  indices of objects to be GCed (ivector), counter of label index)
; -- repl info
; (pointer to the next info, index of the labels, offset of operand)

(import "rlvm-compile")
(import "rowl1-types")

(var code-base `(

(export fun make_assembler () (
    (return (variant @AssemblerE 4 (make_cvector 0) (make_ivector 0) 0 (make_ivector 0) 0))
    ))

(export fun get_code (c) (
    (if (field_get c 3) (replace_labels c))
    (var code (field_get c 1))
    (var indices (field_get c 4))
    (if (> (ivector_size indices) 0)
        (return (bytecode (cvector_raw code) (cvector_size code)
                        (ivector_raw indices) (ivector_size indices)))
        (return (bytecode (cvector_raw code) (cvector_size code) 0 0))
        )
    ))

(export fun set_code (b c) (
    (if (field_get c 3) (replace_labels c))
    (var code (field_get c 1))
    (var indices (field_get c 4))
    (if (> (ivector_size indices) 0)
        (bytecode_set b (cvector_raw code) (cvector_size code)
                        (ivector_raw indices) (ivector_size indices))
        (bytecode_set b (cvector_raw code) (cvector_size code) 0 0)
        )
    ))

(fun get_address (c) (
    (return (cvector_size (field_get c 1)))
    ))

; aliases
(export fun put_nil (c) ((put_imm_i0 c)))
(export fun put_car (c) ((put_field_get0 c)))
(export fun put_cdr (c) ((put_field_get1 c)))
(export fun put_setcar (c) ((put_field_set0 c)))
(export fun put_setcdr (c) ((put_field_set1 c)))

(export fun put_expr_head (c) ((put_field_get c @ExprHead)))
(export fun put_expr_args (c code) ((put_field_get c @ExprArgs)))

(export fun put_code (c code) (
    ; setup object indices
    (var offs (cvector_size (field_get c 1)))
    (var ivec (field_get c 4))
    (var iarr (field_get code 2))
    (var nidx (field_get code 3))
    (for i 0 nidx (do
        (ivector_pushback ivec (+ offs (array_get int iarr i)))
        ))

    ; copy bytecode
    (var seq (field_get code 0))
    (var len (field_get code 1))
    (var cvec (field_get c 1))
    (for i 0 len (do
        (cvector_pushback cvec (array_get char seq i))
        ))
    ))

(export fun put_imm_int (c value) (
    (if (== value -1) (do
        (put_imm_im1 c)
        return
        ))
    (tswitch value (
        (0 . (do (put_imm_i0 c) return))
        (1 . (do (put_imm_i1 c) return))
        (2 . (do (put_imm_i2 c) return))
        (3 . (do (put_imm_i3 c) return))
        (4 . (do (put_imm_i4 c) return))
        (5 . (do (put_imm_i5 c) return))
        (default . break)
        ))
    (if (&& (<= -32768 value) (<= value 32767))
        (put_imm_int16 c value)
        (put_imm_int32 c value)
        )
    ))

(export fun put_arg (c idx) (
    (tswitch idx (
        (0 . (do (put_arg0 c) return))
        (1 . (do (put_arg1 c) return))
        (2 . (do (put_arg2 c) return))
        (3 . (do (put_arg3 c) return))
        (4 . (do (put_arg4 c) return))
        (default . (do
            (put_loadl c (* 4 (+ 2 idx)))
            return
            ))
        ))
    ))

(export fun put_store_arg (c idx) (
    (put_loadl c (* 4 (+ 2 idx)))
    ))

(export fun put_storel (c idx) (
    (tswitch idx (
        (0 . (do (put_storel0 c) return))
        (1 . (do (put_storel1 c) return))
        (2 . (do (put_storel2 c) return))
        (3 . (do (put_storel3 c) return))
        (4 . (do (put_storel4 c) return))
        (5 . (do (put_storel5 c) return))
        (default . (do
            (put_storel c (* -4 (+ 1 idx)))
            return
            ))
        ))
    ))

(export fun put_loadl (c idx) (
    (tswitch idx (
        (0 . (do (put_loadl0 c) return))
        (1 . (do (put_loadl1 c) return))
        (2 . (do (put_loadl2 c) return))
        (3 . (do (put_loadl3 c) return))
        (4 . (do (put_loadl4 c) return))
        (5 . (do (put_loadl5 c) return))
        (default . (do
            (put_loadl c (* -4 (+ 1 idx)))
            return
            ))
        ))
    ))

(export fun put_field_get (c idx) (
    (tswitch idx (
        (0 . (do (put_field_get0 c) return))
        (1 . (do (put_field_get1 c) return))
        (2 . (do (put_field_get2 c) return))
        (3 . (do (put_field_get3 c) return))
        (4 . (do (put_field_get4 c) return))
        (5 . (do (put_field_get5 c) return))
        (default . (do
            (put_field_get c idx)
            return
            ))
        ))
    ))

(export fun put_field_set (c idx) (
    (tswitch idx (
        (0 . (do (put_field_set0 c) return))
        (1 . (do (put_field_set1 c) return))
        (2 . (do (put_field_set2 c) return))
        (3 . (do (put_field_set3 c) return))
        (4 . (do (put_field_set4 c) return))
        (5 . (do (put_field_set5 c) return))
        (default . (do
            (put_field_set c idx)
            return
            ))
        ))
    ))

(export fun emit_byte (c v) (
    (cvector_pushback (field_get c 1) v)
    ))

(export fun emit_short (c v) (
    (var vec (field_get c 1))
    (cvector_pushback vec (& v 0xff))
    (cvector_pushback vec (>> v 8))
    ))

(export fun emit_int (c v) (
    (var vec (field_get c 1))
    (cvector_pushback vec (& v 0xff))
    (cvector_pushback vec (& (>> v 8) 0xff))
    (cvector_pushback vec (& (>> v 16) 0xff))
    (cvector_pushback vec (& (>> v 24) 0xff))
    ))

(fun emit_prim (c v) (
    (emit_short c (& v 0xffff))
    ))

(fun emit_object (c obj) (
    (var offs (cvector_size (field_get c 1)))
    (ivector_pushback (field_get c 4) offs)
    (emit_int c obj)
    ))

(export fun fresh_label (c) (
    (var n (field_get c 5))
    (field_set c 5 (+ n 1))
    (ivector_pushback (field_get c 2) -1)
    (return n)
    ))

(export fun set_label (c v) (
    (var addr (get_address c))
    (ivector_put (field_get c 2) v addr)
    ))

(export fun emit_label (c v) (
    (var offs (get_address c))
    (var info (struct 1 (field_get c 3) v offs (- offs 1)))
    (field_set c 3 info)
    (emit_short c 0)
    ))

(export fun emit_tswitch_label (c v) (
    (var offs (get_address c))
    (var info (struct 1 (field_get c 3) v offs offs))
    (field_set c 3 info)
    (emit_short c 0)
    ))

(export fun emit_lswitch_label (c v) (
    (var offs (get_address c))
    (var info (struct 1 (field_get c 3) v offs (- offs 4)))
    (field_set c 3 info)
    (emit_short c 0)
    ))

(fun replace_labels (c) (
    (var repls (field_get c 3))
    (var vec (field_get c 1))
    (var labels (field_get c 2))
    (while repls (do
        (var label (field_get repls 1))
        (var pos   (field_get repls 2))
        (var offs  (field_get repls 3))
        (if (>= label (ivector_size labels)) (error "undefined label"))
        (var addr (ivector_at labels label))
        (-= addr offs)
        (cvector_put vec pos (& addr 0xff))
        (cvector_put vec (+ pos 1) (>> addr 8))
        (= repls (field_get repls 0))
        ))
    (field_set c 3 0)
    ))

    ))

(var put_func `(
    (byte   . emit_byte)
    (short  . emit_short)
    (ushort . emit_short)
    (int    . emit_int)
    (prim   . emit_prim)
    (object . emit_object)
    (addr   . emit_label)
    ))

(var code 0)
(foreach i vm_instructions (do
    (var args `(c))
    (var body `((emit_byte c @code)))
    (var name (symbol2s (car i)))
    (var operands (cadr i))
    (var j 0)
    (foreach opd operands (do
        (var arg (s++ `arg j))
        (push body `(@(assoc opd put_func) c @arg))
        (push args arg)
        (incr j)
        ))
    (= body (reverse body))
    (= args (reverse args))
    (push code-base `(export fun @(s++ `put_ name) @args @body))
    (incr code)
    ))

(compile `object code-base)
